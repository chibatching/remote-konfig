apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

version = "0.2.0"

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "com.google.firebase:firebase-config-ktx:$firebaseVersion"

    testImplementation 'junit:junit:4.13.2'
}

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.6.0"
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set('javadoc')
    from dokkaJavadoc.outputDirectory
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


def siteUrl = 'https://github.com/chibatching/remote-konfig'
def gitUrl = 'https://github.com/chibatching/remote-konfig.git'

group = "com.github.chibatching"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                artifact javadocJar
                artifact sourcesJar

                pom {
                    artifactId "remote-konfig"
                    name = "remote-konfig"

                    packaging 'aar'
                    url = siteUrl

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'chibatching'
                            name = 'Takao Chiba'
                            email = 'takao.chiba@gmail.com'
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}
